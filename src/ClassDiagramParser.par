/~ Class Diagram Grammar ~/

[*

    var parsedAttributes = [];
    var parsedClassDiagram;
    var parsedClassOffset;
    var parsedConstant;
    var parsedEndPoints = [];
    var parsedMulti;
    var parsedOperations = [];
    var parsedPropKeys = [];
    var parsedProperties = { };

*]

!   '[ \t\r]'
    ;


    'assoc'
    'class'
    'extends'
    'has'
    'implements'
    'nav-to'
    'owns'
    'to'
    'uses'
    '\('
    '\)'
    ','
    '\n'
    '--'
    '='
    '\.\.'
    '\*'
    '[0-9]+'                        DIGITS
    '[A-Za-z_][A-Za-z0-9_]*'        IDENT
    '[-\+#][^\n]*'                  MEMBER
    ;

##

stmt-list           : stmt-list stmt
                    |
                    ;

stmt                : class-def
                    | assoc-def
                    | '\n'
                    ;

class-def           : 'class' IDENT '\n' properties attrs-ops

[*
    var currentClass = parsedClassDiagram.class(%2, 0, 0);

    for (var i = 0; i < parsedAttributes.length; i++) {
        currentClass.attribute(parsedAttributes[i]);
    }

    for (var i = 0; i < parsedOperations.length; i++) {
        currentClass.operation(parsedOperations[i]);
    }

    for (var i = 0; i < parsedPropKeys.length; i++) {
        var key = parsedPropKeys[i];
        currentClass.property(key, parsedProperties[key]);
    }

    for (var key in parsedPropKeys) {
        currentClass[key] = parsedProperties[key];
    }

    parsedAttributes = [];
    parsedOperations = [];
    parsedPropKeys = [];
    parsedProperties = { };
*]
                    ;

properties          : '(' property-list ')' '\n'
                    |
                    ;

property-list       : property-list ',' property
                    | property
                    ;

property            : IDENT '=' constant                        [* parsedPropKeys.push(%1); parsedProperties[%1] = parsedConstant; *]
                    ;

constant            : DIGITS                                    [* parsedConstant = Number(%1); *]
                    ;

attrs-ops           : '--' '\n' attr-list
                    | '--' '\n' attr-list '--' '\n' op-list
                    |
                    ;

attr-list           : attr-list attr
                    |
                    ;

attr                : MEMBER '\n'                               [* parsedAttributes.push(%1); *]
                    ;

op-list             : op-list op
                    |
                    ;

op                  : MEMBER '\n'                               [* parsedOperations.push(%1); *]
                    ;

assoc-def           : 'assoc' end-point assoc-type end-point '\n'

[*
    var ep1 = parsedEndPoints.shift();
    var fromClass = parsedClassDiagram.findClass(ep1[0]);
    var ep2 = parsedEndPoints.shift();
    var toClass = parsedClassDiagram.findClass(ep2[0]);
    parsedClassDiagram.associationFrom(fromClass, ep1[1]).to(toClass, ep2[1], %3);
    parsedEndPoints = [];
*]

                    ;

end-point           : IDENT multi                               [* parsedEndPoints.push([ %1, parsedMulti ]); *]
                    | IDENT                                     [* parsedEndPoints.push([ %1, '' ]); *]
                    ;

multi               : '(' DIGITS ')'                            [* parsedMulti = %2; *]
                    | '(' DIGITS '..' DIGITS ')'                [* parsedMulti = %2 + %3 + %4; *]
                    | '(' DIGITS '..' '*' ')'                   [* parsedMulti = %2 + %3 + %4; *]
                    ;

assoc-type          : 'extends'
                    | 'has'
                    | 'implements'
                    | 'nav-to'
                    | 'owns'
                    | 'to'
                    | 'uses'
                    ;





